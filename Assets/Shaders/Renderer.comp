#version 460
#define LOCAL_INVOCATION 8
layout(local_size_x = LOCAL_INVOCATION) in;
layout(local_size_y = LOCAL_INVOCATION) in;
layout(local_size_z = 1) in;
//test comment
#define MAX_STEPS 100
#define MAX_DIST 100.
#define SURF_DIST .01

layout(set = 0, binding = 0) readonly uniform RenderInfo {
  uint renderWidth;
  uint renderHeight;
  //view matrix
} renderInfo;
layout(set = 0, binding = 1) buffer Render {
  vec4 pixels[];
};

layout(set = 1, binding = 0) readonly buffer MeshCounts {
  uint meshCounts[];  
};
layout(set = 1, binding = 1) readonly buffer MeshData {
  float meshData[];
};
struct IndexStruct {
  uint position;
  uint texture;
  uint normal;
};
layout(set = 1, binding = 2) readonly buffer MeshIndices {
  IndexStruct meshIndices[];
};
struct Face
{
  vec4 center;
  uint indices[4];
};
layout(set = 1, binding = 3) readonly buffer MeshFaces {
  Face meshFaces[];
};
struct Node {
  vec4 center;
  float radius;
  uint left;
  uint right;
  int index;
};
layout(set = 1, binding = 4) readonly buffer MeshNodes {
  Node meshNodes[];
};

vec3 GetPosition(uint meshId, uint index)
{
  uint offset = 0 + (index * 3);
  for (uint i = 0; i < meshId * 5; i++)
    offset += meshCounts[i];
  return vec3(meshData[index + 0], meshData[index + 1], meshData[index + 2]);
}
vec2 GetTexture(uint meshId, uint index)
{
  uint offset = meshCounts[0] + (index * 2);
  for (uint i = 0; i < meshId * 5; i++)
    offset += meshCounts[i];
  return vec2(meshData[offset + 0], meshData[offset + 1]);
}
vec3 GetNormal(uint meshId, uint index)
{
  uint offset = meshCounts[0] + meshCounts[1] + (index * 3);
  for (uint i = 0; i < meshId * 5; i++)
    offset += meshCounts[i];
  return vec3(meshData[offset + 0], meshData[offset + 1], meshData[offset + 2]);
}

float SphereDist(vec3 position, float radius, vec3 point) {
  return length(point - position) - radius;
}
float GetMinDistance(vec3 point) {
  float rtn = 1 / 0.;
  for (uint i = 0; i < meshCounts.length(); i += 5) {
    Node node = meshNodes[meshCounts[i + 4] - 1];
    float dist = SphereDist(vec3(node.center), node.radius, point);
    if (rtn > dist) {
      rtn = dist;
    }
  }
  return rtn;
}
float RayMarch(vec3 rayOrigin, vec3 rayDirection) {
  float rayTravelDist = 0.;
  for (int i = 0; i < MAX_STEPS; i++) {
    vec3 rayLocation = rayOrigin + (rayTravelDist * rayDirection);
    float dist = GetMinDistance(rayLocation);
    rayTravelDist += dist;
    if (dist < SURF_DIST || rayTravelDist > MAX_DIST)
      break;
  }
  return rayTravelDist;
}
void main() {
  vec2 iResolution = vec2(renderInfo.renderWidth, renderInfo.renderHeight);
  uint index = uint((gl_GlobalInvocationID.y * iResolution.x) + gl_GlobalInvocationID.x);
  if (index >= iResolution.x * iResolution.y)
    return;

  vec2 uv = (gl_GlobalInvocationID.xy - .5 * iResolution.xy) / iResolution.y;
  vec3 rayOrigin = vec3(0, 0, -3.);//renderInfo.rayOrigin.xyz;
  vec3 rayDirection = normalize(vec3(uv, 1.));
  float dist = RayMarch(rayOrigin, rayDirection);
  vec3 col = vec3(dist / 3.);
  pixels[index] = vec4(col, 1.);
}